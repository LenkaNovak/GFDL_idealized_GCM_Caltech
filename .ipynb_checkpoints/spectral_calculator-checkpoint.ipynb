{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "\"\"\"\n",
    "    power_spectrum_1d(::AtmosGCMConfigType, var_grid, z, lat, lon, weight)\n",
    "Calculates the 1D (zonal) power spectra using the fourier transform at each latitude and level from a 3D velocity field.\n",
    "The snapshots of these spectra should be averaged to obtain a time-average.\n",
    "The input velocities must be interpolated to a Gaussian grid.\n",
    "# References\n",
    "- [Wiin1967](@cite)\n",
    "- [Koshyk2001](@cite)\n",
    "# Arguments\n",
    "- var_grid: variable (typically u or v) on a Gausian (lon, lat, z) grid to be transformed\n",
    "- z: vertical coordinate (height or pressure)\n",
    "- lat: latitude\n",
    "- lon: longitude\n",
    "- mass_weight: for mass-weighted calculations\n",
    "\"\"\"\n",
    "function power_spectrum_1d(::AtmosGCMConfigType, var_grid, z, lat, lon, weight)\n",
    "    num_lev = length(z)\n",
    "    num_lat = length(lat)\n",
    "    num_lon = length(lon)\n",
    "    num_fourier = Int64(num_lon)\n",
    "\n",
    "    # get number of positive Fourier coefficients incl. 0\n",
    "    if mod(num_lon, 2) == 0 # even\n",
    "        num_pfourier = div(num_lon, 2)\n",
    "    else # odd\n",
    "        num_pfourier = div(num_lon, 2) + 1\n",
    "    end\n",
    "\n",
    "    zon_spectrum = zeros(Float64, num_pfourier, num_lat, num_lev)\n",
    "    freqs = zeros(Float64, num_pfourier, num_lat, num_lev)\n",
    "\n",
    "    for k in 1:num_lev\n",
    "        for j in 1:num_lat\n",
    "            # compute fft frequencies for each latitude\n",
    "            x = lon ./ 180.0 .* π\n",
    "            dx = (lon[2] - lon[1]) ./ 180.0 .* π\n",
    "\n",
    "            freqs_ = fftfreq(num_fourier, 1.0 / dx) # 0,+ve freq,-ve freqs (lowest to highest)\n",
    "            freqs[:, j, k] = freqs_[1:num_pfourier] .* 2.0 .* π\n",
    "\n",
    "            # compute the fourier coefficients for all latitudes\n",
    "            fourier = fft(var_grid[:, j, k]) # e.g. vcos_grid, ucos_grid\n",
    "            fourier = (fourier / num_fourier)\n",
    "\n",
    "            # convert to energy spectra\n",
    "            zon_spectrum[1, j, k] =\n",
    "                zon_spectrum[1, j, k] +\n",
    "                weight[k] * fourier[1] .* conj(fourier[1])\n",
    "\n",
    "            for m in 2:num_pfourier\n",
    "                zon_spectrum[m, j, k] =\n",
    "                    zon_spectrum[m, j, k] +\n",
    "                    2.0 * weight[k] * fourier[m] * conj(fourier[m]) # factor 2 for neg freq contribution\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    return zon_spectrum, freqs\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    power_spectrum_2d(::AtmosGCMConfigType, var_grid, mass_weight)\n",
    "- transform variable on grid to the 2d spectral space using fft on latitude circles\n",
    "(as for the 1D spectrum) and Legendre polynomials for meridians, and calculate spectra\n",
    "# Arguments\n",
    "- var_grid: variable (typically u or v) on a Gausian (lon, lat, z) grid to be transformed\n",
    "- mass_weight: weight for mass-weighted calculations\n",
    "# References\n",
    "- [Baer1972](@cite)\n",
    "\"\"\"\n",
    "function power_spectrum_2d(::AtmosGCMConfigType, var_grid, mass_weight)\n",
    "    #  initialize spherical mesh variables\n",
    "    nθ, nd = (size(var_grid, 2), size(var_grid, 3))\n",
    "    mesh = SpectralSphericalMesh(nθ, nd)\n",
    "    var_spectrum = mesh.var_spectrum\n",
    "    var_spherical = mesh.var_spherical\n",
    "\n",
    "    sinθ, wts = compute_gaussian!(mesh.nθ) # latitude weights using Gaussian quadrature, to orthogonalize Legendre polynomials upon summation\n",
    "    mesh.qnm =\n",
    "        compute_legendre!(mesh.num_fourier, mesh.num_spherical, sinθ, mesh.nθ) #  normalized associated Legendre polynomials\n",
    "\n",
    "    for k in 1:(mesh.nd)\n",
    "        # apply Gaussian quadrature weights\n",
    "        for i in 1:(mesh.nθ)\n",
    "            mesh.qwg[:, :, i] .= mesh.qnm[:, :, i] * wts[i] * mass_weight[k]\n",
    "        end\n",
    "\n",
    "        # Transform variable using spherical harmonics\n",
    "        var_spherical[:, :, k, :] =\n",
    "            trans_grid_to_spherical!(mesh, var_grid[:, :, k]) # var_spherical[m,n,k,sinθ]\n",
    "\n",
    "        # Calculate energy spectra\n",
    "        var_spectrum[:, :, k] =\n",
    "            2.0 .* sum(var_spherical[:, :, k, :], dims = 3) .*\n",
    "            conj(sum(var_spherical[:, :, k, :], dims = 3))  # var_spectrum[m,n,k] # factor 2 to account for negative Fourier frequencies\n",
    "        var_spectrum[1, :, k] = var_spectrum[1, :, k] ./ 2.0 # m=0\n",
    "    end\n",
    "    return var_spectrum, mesh.wave_numbers, var_spherical, mesh\n",
    "end\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.2",
   "language": "julia",
   "name": "julia-1.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
